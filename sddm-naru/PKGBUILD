# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

_pkgname=sddm
_pkgver=0.20.0
pkgname="${_pkgname}-naru"
pkgver="${_pkgver}+pamautounlock"
pkgrel=2
pkgdesc='QML based X11 and Wayland display manager'
arch=(x86_64)
url='https://github.com/sddm/sddm'
license=(GPL)
depends=(qt5-declarative xorg-xauth xorg-server ttf-font)
makedepends=(extra-cmake-modules python-docutils qt5-tools)
backup=('usr/share/sddm/scripts/Xsetup'
        'usr/share/sddm/scripts/Xstop'
        'etc/pam.d/sddm'
        'etc/pam.d/sddm-autologin'
        'etc/pam.d/sddm-greeter')
provides=("$_pkgname" display-manager)
conflicts=("$_pkgname")
source=(https://github.com/$_pkgname/$_pkgname/archive/v$_pkgver/$_pkgname-$_pkgver.tar.gz
		0001-Add-pam_sddm.patch
		0002-Edit-systemd-units-and-pam-confs-for-pam_sddm-to-wor.patch)
sha256sums=('467c8bd91e0d4f76d4bc4e57ab334f1f48a33478482ba9476d93b0b3d7ace9fb'
			'1884adc365e907c0fd5d305eef02321d24a9d071e58c74069acdd0b60c5fd74b'
			'81d37e893a5e01fde3f2a8f24085b520a2a40fe236f54431b4a0c0de35685751')

prepare() {
	patch -d $_pkgname-$_pkgver -p1 < 0001-Add-pam_sddm.patch
	patch -d $_pkgname-$_pkgver -p1 < 0002-Edit-systemd-units-and-pam-confs-for-pam_sddm-to-wor.patch
}

build() {
  cmake -B build -S $_pkgname-$_pkgver \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_INSTALL_LIBEXECDIR=/usr/lib/sddm \
        -DDBUS_CONFIG_DIR=/usr/share/dbus-1/system.d \
        -DDBUS_CONFIG_FILENAME=sddm_org.freedesktop.DisplayManager.conf \
        -DBUILD_MAN_PAGES=ON \
        -DUID_MAX=60513
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build

  install -d "$pkgdir"/usr/lib/sddm/sddm.conf.d
  "$pkgdir"/usr/bin/sddm --example-config > "$pkgdir"/usr/lib/sddm/sddm.conf.d/default.conf
# Don't set PATH in sddm.conf
  sed -r 's|DefaultPath=.*|DefaultPath=/usr/local/sbin:/usr/local/bin:/usr/bin|g' -i "$pkgdir"/usr/lib/sddm/sddm.conf.d/default.conf
# Unset InputMethod https://github.com/sddm/sddm/issues/952
  sed -e "/^InputMethod/s/qtvirtualkeyboard//" -i "$pkgdir"/usr/lib/sddm/sddm.conf.d/default.conf
}

